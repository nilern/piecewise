use lexer::{Tok, LexicalError, SrcPos};
use ast::CST;

use std::str::FromStr;

#[LALR] grammar;

Comma<T>: Vec<T> = {
    <v: (<T> ",")*> <e: T?> => match e {
        None => v,
        Some(e) => {
            let mut v = v;
            v.push(e);
            v
        }
    }
};

CommaT<T>: Vec<T> = {
    <v: (<T> ",")+> => v,
    "," => vec![]
};

CommaPlus<T>: Vec<T> = {
    <v: (<T> ",")*> <e: T> => {
        let mut v = v;
        v.push(e);
        v
    }
};

SemiColon<T>: Vec<T> = {
    <v: (<T> ";")*> <e: T?> => match e {
        None => v,
        Some(e) => {
            let mut v = v;
            v.push(e);
            v
        }
    }
};

pub Exprs: CST = {
    <stmts: SemiColon<Stmt>> => CST::Block(stmts)
};

pub Expr: CST = {
    <Stmt>
};

Stmt: CST = {
    <lhs: Infix> "=>" <rhs: Infix> => CST::Params(Box::new(lhs), Box::new(rhs)),
    <lhs: Infix> "=" <rhs: Infix> => CST::Def(Box::new(lhs), Box::new(rhs)),
    <Infix>
};

Infix: CST = {
    <l: Infix> <op: Op> <r: App> => CST::App(Box::new(CST::Symbol(op)), vec![l, r]),
    <App>
};

App: CST = {
    <f: Simple> <args: Simple+> => CST::App(Box::new(f), args),
    <Simple>
};

Simple: CST = {
    "{" <Exprs> "}",
    "(" <Expr> ")",
    <Coll>,
    <Atom>
};

Coll: CST = {
    "(" <elems: CommaT<Expr>> ")"     => CST::Tuple(elems),
    "[" <elems: Comma<Expr>> "]"     => CST::Array(elems)//,
    //"{" <elems: CommaPlus<Expr>> "}" => CST::Set(elems),
    //"{" <kvs: Comma<MapPair>> "}"    => CST::Map(kvs)
};

Atom: CST = {
    Name => CST::Symbol(<>),
    Number => CST::Int(isize::from_str(&<>).unwrap())
};

//MapPair: (CST, CST) = {
//    <key: Expr> ":" <val: Expr> => (key, val)
//};

extern {
    type Location = SrcPos;
    type Error = LexicalError;
    enum Tok {
        Name => Tok::Name(<String>),
        Op => Tok::Op(<String>),
        Number => Tok::Number(<String>),
        "(" => Tok::LParen,
        ")" => Tok::RParen,
        "[" => Tok::LBracket,
        "]" => Tok::RBracket,
        "{" => Tok::LBrace,
        "}" => Tok::RBrace,
        "," => Tok::Comma,
        ";" => Tok::Semicolon,
        "=" => Tok::Eq,
        "=>" => Tok::Arrow
    }
}
