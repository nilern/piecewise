datatype blockItem = Clause of CST0.expr vector * CST0.expr option * CST0.stmt
                   | Stmt of CST0.stmt

fun opApp pos opPos opStr l r =
        CST0.FixE (Expr0.App (pos,
                              CST0.FixE (Expr0.Var (opPos,
                                                    Var.Lex
                                                        (Name.fromString
                                                             opStr))),
                   Vector.fromList [l, r]))

fun applyName pos name exprs =
    CST0.FixE (Expr0.App(pos, CST0.FixE (Expr0.Var (pos, Var.Lex name)), exprs))

fun applyNameRev pos name revExprs =
    applyName pos name (Vector.fromList (List.rev revExprs))

fun extractApp _ [e] = e
  | extractApp pos (f::args) =
    CST0.FixE (Expr0.App (pos, f, Vector.fromList args))
  | extractApp _ [] = raise LrParser.ParseError

fun extractBlock pos (items as (Clause _ :: _)) =
        let fun stmtSplit (Stmt stmt::items) =
                    let val (stmts', items') = stmtSplit items
                    in (stmt::stmts', items') end
              | stmtSplit items = ([], items)
            fun clauses (Clause(formals, cond, stmt)::items) =
                    let val (stmts, items') = stmtSplit items
                        val pos = CST0.exprPos (Vector.sub(formals, 0))
                        val tuple = Name.fromString "tuple"
                    in (CST0.Bind (applyName pos tuple formals, cond),
                        CST0.FixE (Expr0.Block(CST0.stmtPos stmt,
                                   Vector.fromList (stmt::stmts))))
                       ::clauses items end
              | clauses [] = []
              | clauses _ = raise LrParser.ParseError
        in CST0.FixE (Expr0.Fn (pos, Name.freshFromString "formals",
                                Vector.fromList (clauses items))) end
  | extractBlock pos (items as (Stmt _ :: _)) =
        let fun unwrapStmt (Stmt stmt) = stmt
              | unwrapStmt (Clause _) = raise LrParser.ParseError
        in CST0.FixE
               (Expr0.Block (pos,
                             Vector.map unwrapStmt (Vector.fromList items)))
        end
  | extractBlock _ [] = raise LrParser.ParseError

%%

%name Pcws

%pos Pos.t

%term LEXID of string
    | DYNID of string
    | OP0 of string
    | OP1 of string
    | OP2 of string
    | OP3 of string
    | OP4 of string
    | OP5 of string
    | OP6 of string
    | OP7 of string
    | PRIM of string

    | INT of string
    | STRING of string
    | CHAR of string

    | EQ
    | AUG
    | DARROW
    | ARROW

    | LPAREN
    | RPAREN
    | LBRACKET
    | RBRACKET
    | LBRACE
    | RBRACE

    | COMMA
    | SEMI

    | EOF

%nonterm program of CST0.stmt vector
       | stmt of CST0.stmt
       | expr of CST0.expr
       | blockItem of blockItem

       | infix0 of CST0.expr
       | infix1 of CST0.expr
       | infix2 of CST0.expr
       | infix3 of CST0.expr
       | infix4 of CST0.expr
       | infix5 of CST0.expr
       | infix6 of CST0.expr
       | infix7 of CST0.expr
       | app of CST0.expr list
       | primApp of CST0.expr
       | simple of CST0.expr

       | datum of CST0.expr
       | prim of CST0.expr
       | compound of CST0.expr

       | exprList of CST0.expr list
       | exprListTwoPlus of CST0.expr list
       | mapPairs of (CST0.expr * CST0.expr) list
       | stmtList of CST0.stmt list
       | blockItemList of blockItem list

%eop EOF
%noshift EOF

%%

program : stmtList (Vector.fromList (List.rev stmtList))

stmt : expr EQ expr  (CST0.FixS (Stmt0.Def (CST0.Bind (expr1, NONE), expr2)))
     | expr AUG expr (CST0.FixS (Stmt0.AugDef (CST0.Bind (expr1, NONE), expr2)))
     | expr          (CST0.FixS (Stmt0.Expr expr))

blockItem : app DARROW stmt
            (Clause (Vector.fromList (List.rev app), NONE, stmt))
          | stmt (Stmt stmt)

expr : infix0 (infix0)

infix0 : infix0 OP0 infix1 (opApp infix0left OP0left OP0 infix0 infix1)
       | infix1            (infix1)
infix1 : infix1 OP1 infix2 (opApp infix1left OP1left OP1 infix1 infix2)
       | infix2            (infix2)
infix2 : infix2 OP2 infix3 (opApp infix2left OP2left OP2 infix2 infix3)
       | infix3            (infix3)
infix3 : infix3 OP3 infix4 (opApp infix3left OP3left OP3 infix3 infix4)
       | infix4            (infix4)
infix4 : infix4 OP4 infix5 (opApp infix4left OP4left OP4 infix4 infix5)
       | infix5            (infix5)
infix5 : infix5 OP5 infix6 (opApp infix5left OP5left OP5 infix5 infix6)
       | infix6            (infix6)
infix6 : infix6 OP6 infix7 (opApp infix6left OP6left OP6 infix6 infix7)
       | infix7            (infix7)

infix7 : infix7 OP7 app
            (opApp infix7left OP7left
                   OP7 infix7 (extractApp appleft(List.rev app)))
       | app                (extractApp appleft(List.rev app))
       | infix7 OP7 primApp (opApp infix7left OP7left OP7 infix7 primApp)
       | primApp            (primApp)

app : simple     ([simple])
    | app simple (simple :: app)

primApp : PRIM app (CST0.FixE (Expr0.PrimApp (PRIMleft, Primop.fromString PRIM,
                                 Vector.fromList (List.rev app))))

simple : LPAREN expr RPAREN (expr)
       | LBRACE blockItemList RBRACE
         (extractBlock LBRACEleft (List.rev blockItemList))
       | LBRACKET stmtList RBRACKET
         (let val bind =
                  CST0.Bind
                      (applyNameRev
                           LBRACKETleft (Name.fromString "tuple") [],
                       NONE)
          in CST0.FixE (Expr0.Fn (LBRACKETleft,
                  Name.freshFromString "formals",
                  Vector.fromList [
                      (bind, CST0.FixE (Expr0.Block (LBRACKETleft,
                                 (Vector.fromList (List.rev stmtList)))))]))
          end)
       | LEXID (CST0.FixE (Expr0.Var (LEXIDleft,
                                      Var.Lex (Name.fromString LEXID))))
       | DYNID (CST0.FixE (Expr0.Var (DYNIDleft,
                                      Var.Dyn (Name.fromString DYNID))))
       | datum (datum)

datum : prim     (prim)
      | compound (compound)

prim : INT    (CST0.FixE (Expr0.Const (INTleft, Const.Int INT)))
     | STRING (CST0.FixE (Expr0.Const (STRINGleft, Const.String STRING)))
     | CHAR   (CST0.FixE (Expr0.Const (CHARleft, Const.Char CHAR)))

compound : LPAREN exprList RPAREN
           (applyNameRev LPARENleft (Name.fromString "tuple") exprList)
         | LBRACKET exprList RBRACKET
           (applyNameRev LBRACKETleft (Name.fromString "array") exprList)
         | LBRACE exprList RBRACE
           (applyNameRev LBRACEleft (Name.fromString "set") exprList)
         | LBRACE mapPairs RBRACE
           (let val pos = LBRACEleft
                val args = List.foldl (fn ((k, v), acc) => k::v::acc)
                                      [] mapPairs
            in CST0.FixE (Expr0.App(pos,
                                    CST0.FixE
                                        (Expr0.Var (pos,
                                                    Var.Lex
                                                        (Name.fromString
                                                            "hashMap"))),
                       Vector.fromList args)) end)

exprList :                 ([])
         | expr COMMA      ([expr])
         | exprListTwoPlus (exprListTwoPlus)

exprListTwoPlus : expr COMMA expr            ([expr2, expr1])
                | exprListTwoPlus COMMA expr (expr :: exprListTwoPlus)

mapPairs : ARROW                          ([])
         | expr ARROW expr                ([(expr1, expr2)])
         | mapPairs COMMA expr ARROW expr ((expr1, expr2) :: mapPairs)

stmtList : stmt               ([stmt])
         | stmtList SEMI stmt (stmt :: stmtList)

blockItemList : blockItem                    ([blockItem])
              | blockItemList SEMI blockItem (blockItem :: blockItemList)
