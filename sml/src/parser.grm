datatype blockItem = Clause of CST.expr vector * CST.stmt
                   | Stmt of CST.stmt

fun opApp pos opPos opStr l r =
        CST.App (pos, CST.Var (opPos, Var.Lex (Name.Plain opStr)),
                 Vector.fromList [l, r])

fun extractApp _ [e] = e
  | extractApp pos (f::args) = CST.App (pos, f, Vector.fromList args)
  | extractApp _ [] = raise LrParser.ParseError

fun extractBlock pos (items as (Clause _ :: _)) =
        let fun stmtSplit (Stmt stmt::items) =
                    let val (stmts', items') = stmtSplit items
                    in (stmt::stmts', items') end
              | stmtSplit items = ([], items)
            fun clauses (Clause(formals, stmt)::items) =
                    let val (stmts, items') = stmtSplit items
                    in (formals, NONE,
                        CST.Block(CST.stmtPos stmt,
                                  Vector.fromList (stmt::stmts)))
                       ::clauses items end
              | clauses [] = []
              | clauses _ = raise LrParser.ParseError
        in CST.Fn (pos, Vector.fromList (clauses items)) end
  | extractBlock pos (items as (Stmt _ :: _)) =
        let fun unwrapStmt (Stmt stmt) = stmt
              | unwrapStmt (Clause _) = raise LrParser.ParseError
        in CST.Block(pos, Vector.map unwrapStmt (Vector.fromList items)) end
  | extractBlock _ [] = raise LrParser.ParseError

fun applyNameRev pos name revExprs =
        CST.App(pos, CST.Var(pos, Var.Lex (Name.Plain name)),
                Vector.fromList (List.rev revExprs))

%%

%name Pcws

%pos Pos.t

%term LEXID of string
    | DYNID of string
    | OP0 of string
    | OP1 of string
    | OP2 of string
    | OP3 of string
    | OP4 of string
    | OP5 of string
    | OP6 of string
    | OP7 of string
    | PRIM of string

    | NUM of string
    | STRING of string
    | CHAR of string

    | EQ
    | AUG
    | DARROW
    | ARROW

    | LPAREN
    | RPAREN
    | LBRACKET
    | RBRACKET
    | LBRACE
    | RBRACE

    | COMMA
    | SEMI

    | EOF

%nonterm program of CST.stmt vector
       | stmt of CST.stmt
       | expr of CST.expr
       | blockItem of blockItem

       | infix0 of CST.expr
       | infix1 of CST.expr
       | infix2 of CST.expr
       | infix3 of CST.expr
       | infix4 of CST.expr
       | infix5 of CST.expr
       | infix6 of CST.expr
       | infix7 of CST.expr
       | app of CST.expr list
       | simple of CST.expr

       | datum of CST.expr
       | compound of CST.expr

       | exprList of CST.expr list
       | exprListTwoPlus of CST.expr list
       | mapPairs of (CST.expr * CST.expr) list
       | stmtList of CST.stmt list
       | blockItemList of blockItem list

%eop EOF
%noshift EOF

%%

program : stmtList (Vector.fromList (List.rev stmtList))

stmt : expr EQ expr  (CST.Def (expr1, expr2))
     | expr AUG expr (CST.AugDef (expr1, expr2))
     | expr          (CST.Expr expr)

blockItem : app DARROW stmt (Clause (Vector.fromList (List.rev app), stmt))
        | stmt            (Stmt stmt)

expr : infix0 (infix0)

infix0 : infix0 OP0 infix1 (opApp infix0left OP0left OP0 infix0 infix1)
       | infix1            (infix1)
infix1 : infix1 OP1 infix2 (opApp infix1left OP1left OP1 infix1 infix2)
       | infix2            (infix2)
infix2 : infix2 OP2 infix3 (opApp infix2left OP2left OP2 infix2 infix3)
       | infix3            (infix3)
infix3 : infix3 OP3 infix4 (opApp infix3left OP3left OP3 infix3 infix4)
       | infix4            (infix4)
infix4 : infix4 OP4 infix5 (opApp infix4left OP4left OP4 infix4 infix5)
       | infix5            (infix5)
infix5 : infix5 OP5 infix6 (opApp infix5left OP5left OP5 infix5 infix6)
       | infix6            (infix6)
infix6 : infix6 OP6 infix7 (opApp infix6left OP6left OP6 infix6 infix7)
       | infix7            (infix7)

infix7 : infix7 OP7 app (opApp infix7left OP7left
                               OP7 infix7 (extractApp appleft(List.rev app)))
       | app            (extractApp appleft(List.rev app))

app : simple     ([simple])
    | app simple (simple :: app)

simple : LPAREN expr RPAREN (expr)
       | LBRACE blockItemList RBRACE
         (extractBlock LBRACEleft (List.rev blockItemList))
       | LEXID (CST.Var (LEXIDleft, Var.Lex (Name.Plain LEXID)))
       | datum (datum)

datum : compound (compound)

compound : LPAREN exprList RPAREN (applyNameRev LPARENleft "tuple" exprList)
         | LBRACKET exprList RBRACKET
           (applyNameRev LBRACKETleft "array" exprList)
         | LBRACE exprList RBRACE (applyNameRev LBRACEleft "set" exprList)
         | LBRACE mapPairs RBRACE
           (let val pos = LBRACEleft
                val args = List.foldl (fn ((k, v), acc) => k::v::acc)
                                      [] mapPairs
            in CST.App(pos, CST.Var(pos, Var.Lex (Name.Plain "hashMap")),
                       Vector.fromList args) end)

exprList :                 ([])
         | expr COMMA      ([expr])
         | exprListTwoPlus (exprListTwoPlus)

exprListTwoPlus : expr COMMA expr            ([expr2, expr1])
                | exprListTwoPlus COMMA expr (expr :: exprListTwoPlus)

mapPairs : ARROW                          ([])
         | expr ARROW expr                ([(expr1, expr2)])
         | mapPairs COMMA expr ARROW expr ((expr1, expr2) :: mapPairs)

stmtList : stmt               ([stmt])
         | stmtList SEMI stmt (stmt :: stmtList)

blockItemList : blockItem                    ([blockItem])
              | blockItemList SEMI blockItem (blockItem :: blockItemList)
